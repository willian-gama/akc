buildscript {
    ext {
        application_id = "com.android.dev.engineer.kotlin.compose"
        version_name = "1.0"
    }

    dependencies {
        classpath libs.code.analysis
    }
}

plugins {
    alias(libs.plugins.android.application) apply false
    alias(libs.plugins.android.library) apply false
    alias(libs.plugins.kotlin) apply false
    alias(libs.plugins.ksp) apply false
    alias(libs.plugins.paparazzi) apply false
    alias(libs.plugins.hilt) apply false
    alias(libs.plugins.compose.compiler) apply false
}

def localProperties = new Properties()
localProperties.load(file("local.properties").newDataInputStream())

apply plugin: "com.willian.gama.kgp.code.analysis"

code_analysis_params {
    buildType = "debug"
    kotlinVersion = libs.plugins.kotlin.get().version
}

subprojects {
    afterEvaluate {
        if (project.hasProperty('android')) {
            android {
                compileSdk 34

                defaultConfig {
                    minSdk 23
                    targetSdk 34
                }

                compileOptions {
                    sourceCompatibility JavaVersion.VERSION_17
                    targetCompatibility JavaVersion.VERSION_17
                }

                kotlinOptions {
                    jvmTarget = JavaVersion.VERSION_17
                }
            }
        }
    }
}

// Gradle wrapper: https://docs.gradle.org/current/userguide/gradle_wrapper.html#customizing_wrapper
tasks.named('wrapper') {
    description = "Gradle wrapper task to update binary"
    gradleVersion = libs.versions.gradle.get()
    distributionType = Wrapper.DistributionType.BIN
}